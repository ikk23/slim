//INITIALIZE CODE
initialize() {
	defineConstant("CAPACITY", 3242);
	defineConstant("DENSITY_INTERACTION_DISTANCE", 0.002);
	defineConstant("DRIVE_FITNESS_VALUE", 0.9);
	defineConstant("DROP_SIZE", 32);
	defineConstant("DROP_RADIUS", 0.01);
	defineConstant("EMBRYO_RESISTANCE_RATE", 0.0);
	defineConstant("GERMLINE_RESISTANCE_RATE", 0.7);
	defineConstant("GROWTH_AT_ZERO_DENSITY", 6);
	defineConstant("HETEROZYGOUS_DROP", F);
	defineConstant("MALE_ONLY_DROP", T);
	defineConstant("FEMALE_ONLY_DROP", F);
	defineConstant("NUM_GRNAS", 1);
	defineConstant("R1_OCCURRENCE_RATE", 0.0);
	defineConstant("SPEED", 0.008);
	defineConstant("X_SHREDDER", T);
	defineConstant("Y_SHREDDER", F);
	defineConstant("TRACK_BY_CELL", F);
	defineConstant("TRACK_RIPLEYS_L", F);
	defineConstant("NUMBER_OF_CELLS", 64);
	defineConstant("NO_DROP", F);
	// */    // End header.
	
	// Below are constants that probably won't change
	defineConstant("FEMALE_SPEED", SPEED);
	defineConstant("WT_START", F);
	defineConstant("EQUILIBRIUM_BIN_POPULATION", 0.5*CAPACITY/20);
	defineConstant("EQUILIBRIUM_WT_ALLELE_THRESHOLD", 0.8*1.5*CAPACITY); //number of wt X
	defineConstant("GRNA_SATURATION_SIMULATED", F);
	defineConstant("INBREEDING_AVOIDANCE_FACTOR", 0.0);
	defineConstant("MALE_ONLY_PROMOTER", F);
	defineConstant("MALE_MATE_CAP", F);
	defineConstant("MAX_MALE_MATINGS", 10);
	defineConstant("MATING_DISTANCE", SPEED);
	defineConstant("NUM_CUT_PHASES", 1);
	defineConstant("REPRISING_BOUNDARIES", T);
	defineConstant("R2_FITNESS_VALUE", 1.0);
	
	// For mortality rate as a function of age. Some females are allowed to survive two generations:
	defineConstant("RATE_FEMALES_SURVIVE", 0.0);
	
	// This simulation will use a non-Wright-Fisher model.
	initializeSLiMModelType("nonWF");
	
	// This simulation will take place accross continuous space.
	// By default, model occurs in a torroidal space to prevent edge effects.
	// The model tracks pedigrees in order to implement inbreeding avoidance.
	if (REPRISING_BOUNDARIES)
		initializeSLiMOptions(keepPedigrees=T, dimensionality="x");
	else
		initializeSLiMOptions(keepPedigrees=T, dimensionality="x", periodicity="x");
	
	// Four variations: Wild type, drive, r1 resistance, r2 resistance.
	wt = initializeMutationType("m1", 0.5, "f", 0.0);
	dr = initializeMutationType("m2", 0.5, "f", 0.0);
	r1 = initializeMutationType("m3", 0.5, "f", 0.0);
	r2 = initializeMutationType("m4", 0.5, "f", 0.0);
	cut = initializeMutationType("m5", 0.5, "f", 0.0);
	gap = initializeMutationType("m6", 0.5, "f", 0.0);
	variations = c(wt, dr, r1, r2, cut, gap);
	
	// Variations are all part of g1 genome element.
	initializeGenomicElementType("g1", variations, c(1, 1, 1, 1, 1, 1));
	
	// Element is NUM_GRNAS long, each spot on the element representing a GRNA target location.
	initializeGenomicElement(g1, 0, NUM_GRNAS - 1);
	
	// These mutations overwrite one another.
	variations.mutationStackPolicy = "l";
	variations.mutationStackGroup = 1;
	
	// No mutation in this model.
	initializeMutationRate(0.0);
	initializeRecombinationRate(0.0);
	
	// Simulate sexual reproduction: A for autosome.
	//initializeSex("A");
	
	// Mate searching: Default interaction has strength 1 for individuals within max distance, 0 for others.
	initializeInteractionType(1, "x", reciprocal=T, maxDistance=MATING_DISTANCE);
	// Adult competition:
	initializeInteractionType(2, "x", reciprocal=T, maxDistance=DENSITY_INTERACTION_DISTANCE);
	// For now, just modeling interaction with flat strength of 1.
	// i2.setInteractionFunction("n", 1.0, DENSITY_INTERACTION_DISTANCE);

	//use this to track Ripley's L between wt individiauls
	initializeInteractionType(3, "x", reciprocal=T, maxDistance=DENSITY_INTERACTION_DISTANCE);
	
	// Constants based on other constants:
	// Rate of expected competition
	defineConstant("EXPECTED_ADULT_COMPETITION", CAPACITY * 2 * DENSITY_INTERACTION_DISTANCE);
}


//// HELPER FUNCTION FOR FORMING A RESISTANCE ALLELE AT A GIVEN LOCUS.
function (void)addResistanceAllele(o<Genome>$ genome, i$ locus) {
	// Resistance alleles form at a specified rate.	m3 is r1, m4 is r2.
	genome.addNewDrawnMutation(ifelse((runif(1) < R1_OCCURRENCE_RATE), m3,m4), locus);
}


//// FUNCTION FOR CAS9 EFFECTS IN THE GERMLINE.
function (void)germline(o<Genome>$ child_chromosome, o<Individual>$ parent, f$ resistance_rate) {
	// Determine presence of drive in this parent.
	parent_drive_count = sum(parent.genomes.countOfMutationsOfType(m2) == NUM_GRNAS);
	// If parent doesn't have drive allele, nothing happens in germline.
	if (!parent_drive_count)
		return;
	
	// Chromosome must have wt to cut, or nothing happens.
	if (child_chromosome.countOfMutationsOfType(m1) == 0)
		return;
	
	// Using the resistance rate passed to the function,
	// determine the per phase cut rate.
	grna_factor = 1;
	if (GRNA_SATURATION_SIMULATED)
		grna_factor = NUM_GRNAS;
	per_phase_cut_rate = 1 - (1 - resistance_rate)^(1 / (NUM_CUT_PHASES * grna_factor));
	
	// Determine if cas9 cuts the chromosome in the germline.
	casCut(child_chromosome, per_phase_cut_rate);
}


//// FUNCTION FOR CAS9 EFFECTS IN EMBRYO.
function (void)embryo(o<Genome>$ child_chromosome_1, o<Genome>$ child_chromosome_2, o<Individual>$ mother) {
	// Determine presence of drive in this mother.
	mother_drive_count = sum(mother.genomes.countOfMutationsOfType(m2) == NUM_GRNAS);
	// If mother doesn't have drive allele, nothing happens in embryo.
	if (!mother_drive_count)
		return;
	grna_factor = 1;
	if (GRNA_SATURATION_SIMULATED)
		grna_factor = NUM_GRNAS;
	per_phase_cut_rate = 1 - (1 - EMBRYO_RESISTANCE_RATE)^(mother_drive_count / (NUM_CUT_PHASES * grna_factor));
	
	// Then pass this rate as an argument to  the casCut funtion to
	// determine if cas9 cuts in the embryo for each chromosome.
	casCut(child_chromosome_1, per_phase_cut_rate);
	casCut(child_chromosome_2, per_phase_cut_rate);
}


//// FUNCTION FOR CAS9 CUTTING A TARGET CHROMOSOME.
function (void)casCut(o<Genome>$ chromosome, f$ per_phase_cut_rate) {
	// If chromosome has no wild type targets to cut, cas9 does nothing.
	if (chromosome.countOfMutationsOfType(m1) == 0)
		return;
	
	// Vector of possible loci for cas9 to cut:
	wt_loci = chromosome.positionsOfMutationsOfType(m1);
	
	// In each cut phase...
	for (i in seqLen(NUM_CUT_PHASES)) {
		// At each possible locus...
		for (j in seqAlong(wt_loci)) {
			// Check to see if cas9 cuts...
			if (runif(1) < per_phase_cut_rate) {
				// Convert the locus temporarily to an m5,
				// representing that this site is being cut.
				chromosome.addNewDrawnMutation(m5, wt_loci[j]);
			}
		}
		// If any cuts were made...
		if (chromosome.countOfMutationsOfType(m5)) {
			cut_loci = chromosome.positionsOfMutationsOfType(m5);
			// If just one cut is made, create an R1 or R2 resistance allele.
			if (chromosome.countOfMutationsOfType(m5) == 1)
				addResistanceAllele(chromosome, cut_loci[0]);
			else {
				// If multiple cuts are made in the same cut phase, loss of
				// function results. Convert leftmost site to m4 (R2)
				// everthing else through the rightmost site to m6 (cut).
				number_of_cuts = cut_loci[size(cut_loci) - 1] - cut_loci[0];
				for (index in seqLen(number_of_cuts))
					chromosome.addNewDrawnMutation(m6, cut_loci[0] + index + 1);
				chromosome.addNewDrawnMutation(m4, cut_loci[0]);
			}
			// Because some wt loci may have been cut out of the
			// chromosome, remake the vector of possible cut sites.
			if (chromosome.countOfMutationsOfType(m1) == 0)
				return;
			wt_loci = chromosome.positionsOfMutationsOfType(m1);
		}
	}
}


//// HELPER FUNCTION TO OVERWRITE ONE GENOME WITH ANOTHER.
function (void)copy_genome(o<Genome>$ source, o<Genome>$ destination) {
	for (i in seqAlong(source.positionsOfMutationsOfType(m1))) //m1 loci (wt)
		destination.addNewDrawnMutation(m1, source.positionsOfMutationsOfType(m1)[i]);
	for (i in seqAlong(source.positionsOfMutationsOfType(m2))) //m2 loci (dr)
		destination.addNewDrawnMutation(m2, source.positionsOfMutationsOfType(m2)[i]);
	for (i in seqAlong(source.positionsOfMutationsOfType(m3))) //m3 loci (r1)
		destination.addNewDrawnMutation(m3, source.positionsOfMutationsOfType(m3)[i]);
	for (i in seqAlong(source.positionsOfMutationsOfType(m4))) //m4 loci (r2)
		destination.addNewDrawnMutation(m4, source.positionsOfMutationsOfType(m4)[i]);
	for (i in seqAlong(source.positionsOfMutationsOfType(m5))) //m5 loci (cut)
		destination.addNewDrawnMutation(m5, source.positionsOfMutationsOfType(m5)[i]);
	for (i in seqAlong(source.positionsOfMutationsOfType(m6))) //m6 loci (gap)
		destination.addNewDrawnMutation(m6, source.positionsOfMutationsOfType(m6)[i]);
} //end function


//// RULES FOR FITNESS BASED ON GENOTYPE.
function (f$)genotypeFitness(o<Individual>$ ind) {
	// Representation of the value of the two chromosomes. Baseline with check for drive or wt.
	c1_value = ifelse(ind.genome1.countOfMutationsOfType(m2) == NUM_GRNAS, DRIVE_FITNESS_VALUE, 1.0);
	c2_value = ifelse(ind.genome2.countOfMutationsOfType(m2) == NUM_GRNAS, DRIVE_FITNESS_VALUE, 1.0);
	
	// Check both chromosomes for resistance allele.
	if (ind.genome1.countOfMutationsOfType(m4) > 0)
		c1_value = c1_value * R2_FITNESS_VALUE;
	if (ind.genome2.countOfMutationsOfType(m4) > 0)
		c2_value = c2_value * R2_FITNESS_VALUE;
	
	
	return ifelse(X_SHREDDER, c2_value, sqrt(c1_value*c2_value));
}

//// OUTPUT WHEN TRACKING RIPLEY'S L 
// this will output <generation> <wt ind count> <ripley's L> to a csv file
function (void)outputRipleysL([string$ result=""]) {
	wt_counts = sim.getValue("wts");
	L = sim.getValue("L");
	path = getwd() + "/" + asString(getSeed()) + "_" + result + "_shredder_drives_1d_L.csv";
	file = "generation,wt allele count, ripley's l\n";
	
	for (i in seqAlong(wt_counts))
		file = file + asString(i) + ',' + asString(wt_counts[i]) + ',' + asString(L[i]) + '\n';
	
	if (!writeFile(path, file))
		stop("error writing file."); //check if there's an issue
}

//// OUTPUT WHEN TRACKING BY CELL COUNT
// this will output <cell count> <cell x position> <cell y position> <generation> to a csv file
function (void)outputCellCounts([string$ result=""])  {
	path = getwd() + "/" + asString(getSeed()) + "_" + result + "_shredder_drives_1d_counts.csv";
	file = "count, x, y, gen\n";
	hist = sim.getValue("hist");
	
	for (r in 0:(nrow(hist)-1))
		file = file + paste(hist[r,],sep=",") + "\n";
	
	if (!writeFile(path, file))
		stop("error writing file."); //check if there's an issue

}


////REPRODUCTION CALLBACK
reproduction() {
	if (individual.tagF == 1.0) { //only sample females for reproduction
		all_neighbors = i1.nearestNeighbors(individual, subpop.individualCount);
		neighbors = all_neighbors[all_neighbors.tagF == 0.0]; //male neighbors
		if (size(neighbors) == 0)
			return;
		//try to find a suitable mate; 10 tries
		attempt_no = 0;
		while (1) {
			// First, select a random mate.
			mate_no = 0;
			if (size(neighbors) > 1)
				mate_no = rdunif(1, max=size(neighbors) - 1);
			selected_mate = neighbors[mate_no];
			
			// Determine attractiveness of mate.
			mate_attractiveness = genotypeFitness(selected_mate);
			if (selected_mate.pedigreeParentIDs[0] == individual.pedigreeParentIDs[0] & individual.pedigreeParentIDs[0] != -1)
				mate_attractiveness = mate_attractiveness * (1 - INBREEDING_AVOIDANCE_FACTOR);
			
			// Determine if this mate is chosen, based on attractiveness and number of mates so far.
			if (MALE_MATE_CAP) {
				if (selected_mate.getValue("mates") == MAX_MALE_MATINGS)
					catn("this male cannot mate bc cap reached");
				
				if ((runif(1) < mate_attractiveness) & (selected_mate.getValue("mates") < MAX_MALE_MATINGS)) {
					selected_mate.setValue("mates", selected_mate.getValue("mates") + 1); //increment his mate count
					break;  // Select this mate.
				}
			} else { // else just look at male attractiveness
				if (runif(1) < mate_attractiveness)
					break;
			}
			// After ten candidates fail, female gives up.
			attempt_no = attempt_no + 1;
			if (attempt_no == 9)
				return;
		} //end while-loop. Mate has now been selected. 
		
		// Amount of competition faced per expected competition.
		adult_density_force = i2.totalOfNeighborStrengths(individual);
		competition_ratio = adult_density_force / EXPECTED_ADULT_COMPETITION;
		actual_competition_factor = GROWTH_AT_ZERO_DENSITY/ ((GROWTH_AT_ZERO_DENSITY-1) * competition_ratio + 1);
		
		// Offspring number based on binomial distrobution.
		// p for binomial draw. Number of offspring varies with female genes and competition.
		p = genotypeFitness(individual) * actual_competition_factor * 0.04 / (1 + RATE_FEMALES_SURVIVE);
		num_offspring = rbinom(1, 50, p);
		
		for (i in seqLen(num_offspring)) {
			// Add offspring to the subpopulation.
			offspring = subpop.addCrossed(individual, selected_mate);
			if (!isNULL(offspring)) {
				if (REPRISING_BOUNDARIES) {
					// Offspring placement within reprising boundaries.
					do position = individual.spatialPosition + rnorm(1, 0, FEMALE_SPEED);
					while (!individual.subpopulation.pointInBounds(position));
					offspring.setSpatialPosition(position);
				}
				else {
					// Torroidal world offspring placement.
					position = individual.spatialPosition + rnorm(1, 0, FEMALE_SPEED);
					offspring.setSpatialPosition(p1.pointPeriodic(position));
				}
			} //end not is null
		} //end for-loop
	} //end individual tag of 1
}


//modify child differently based on whether or not the father had the drive
1: modifyChild() {
	
	if (parent1.tagF==0.0) { //figure out the father based on which parent had the tag value of 0
		father = parent1;
		paternalGenome = childGenome1;
		mother = parent2;
		maternalGenome = childGenome2;
	} else {
		father = parent2;
		paternalGenome = childGenome2;
		mother = parent1;
		maternalGenome = childGenome1;
	}
	
	//father didn't have drive; no need to call germline resistance
	if (father.countOfMutationsOfType(m2)==0) {
		child.tagF = asFloat(rdunif(1,0,1)); //child gets a random gender
	
	} else {
		
		//depending on whether this is the X or the Y shredder, the drive genome can differ.
		if (father.genome1.countOfMutationsOfType(m2) == NUM_GRNAS) {
			fatherDriveGenome = father.genome1;
			fatherWTGenome = father.genome2;
		} else {
			fatherDriveGenome = father.genome2;
			fatherWTGenome = father.genome1;
		}
		
		//if the child received drive, child is a male.
		child_received_drive = paternalGenome.countOfMutationsOfType(m2) == NUM_GRNAS;
		
		//if the child didn't receive the dr chromosome, child received the father's non-dr chromosome, which could've been cut
		if (!child_received_drive) {
			germline(paternalGenome, father, GERMLINE_RESISTANCE_RATE); //apply germline resistance to the father's X
			
			while (paternalGenome.countOfMutationsOfType(m4) > 0) {
				if (runif(1) < 0.5) {
					paternalGenome.addNewDrawnMutation(m2, seqLen(NUM_GRNAS));
				} else {
					// Otherwise, recreate the non-drive chromosome before germline resistance and undergo germline resistance again
					copy_genome(fatherWTGenome, paternalGenome);
					germline(paternalGenome, father, GERMLINE_RESISTANCE_RATE);
				} //exits while-loop only if r2 doesn't result (wt remains)
			} //end while-loop
		} //end child didn't initially get drive
		
		//update booleans
		child_received_drive = paternalGenome.countOfMutationsOfType(m2)==NUM_GRNAS;
		
		//drive alleles are on the Y-chromosome for the X_SHREDDER drive
		if (X_SHREDDER)
			child.tagF = ifelse(child_received_drive, 0.0, 1.0); //if child got the Y-drive from father, child is male 
		
		else if (Y_SHREDDER)
			child.tagF = ifelse(child_received_drive, 1.0, 0.0); //if child got the X-drive from father, child is female 
	} //end else father had drive
	
	if (Y_SHREDDER)
		embryo(maternalGenome,paternalGenome, mother);
	
	if (paternalGenome.countOfMutationsOfType(m4))
		return F;
	
	if (child.tagF==0.0 & MALE_MATE_CAP)
		child.setValue("mates", 0); //track male matings
	
	return T;
} //end modifyChild


//// COLOR RULES FOR SLIM GUI DISPLAY.
early() {
	all = sim.subpopulations.individuals;
	males = all[all.tagF == 0.0];
	females = all[all.tagF == 1.0];
	males.color = "dodgerblue4"; //wt males are dark green
	females.color = "deepskyblue3"; //wt females are light green
	
	males[males.countOfMutationsOfType(m3) >= NUM_GRNAS].color = "darkgoldenrod1"; // will stay dark gold if male and r1/(not d and not r2)
	females[females.countOfMutationsOfType(m3) >= NUM_GRNAS].color = "gold";  // will stay light gold if female and r1/(not d and not r2)
	
	males[males.countOfMutationsOfType(m4) > 0].color = "purple"; //will stay dark purple if male with no drives and at least one r2
	females[females.countOfMutationsOfType(m4) > 0].color = "mediumpurple1"; //will stay light purple if female with no drives and at least one r2
	
	males[males.countOfMutationsOfType(m2) >= NUM_GRNAS].color = "firebrick"; //dark pink if male with at least one drive
	females[females.countOfMutationsOfType(m2) >= NUM_GRNAS].color = "firebrick1"; //light pink if female with at least one drive
} //end early


//// EVALUATE SPATIAL INTERACTIONS BETWEEN INDIVIDUALS FOR OTHER FUNCTIONS.
late() {
	i1.evaluate();
	i2.evaluate();
	
	all = sim.subpopulations.individuals;
	wt = all.countOfMutationsOfType(m1) == 2*NUM_GRNAS;
	wts = all[wt];
	num_wt = size(wts);
	
	if (TRACK_RIPLEYS_L & sim.generation >= 10) {
		wts_before = sim.getValue("wts");
		sim.setValue("wts", c(wts_before, size(wts)));
		i3.evaluate();
		K = sum(i3.totalOfNeighborStrengths(wts));
		L = sqrt(K/PI) - DENSITY_INTERACTION_DISTANCE;
		catn("GEN: " + sim.generation + " RIPLEY's K: " + K);
		catn("RIPLEY'S L: " + L);
		l_before = sim.getValue("L");
		sim.setValue("L", c(l_before, L)); //store this L	
	}
}

//evaluate interactions between wild-types only if tracking Ripley's L
interaction(i3) {
	if (!TRACK_RIPLEYS_L)
		return;
	
	if ((receiver.countOfMutationsOfType(m1) != 2*NUM_GRNAS) | (exerter.countOfMutationsOfType(m1) != 2*NUM_GRNAS))
		return 0.0;
	
	//receiver and exerter are both wt
	W = sim.getValue("wts");
	W = W[(length(W)-1)];
	return (1/(W^2));
}


//// SURVIVAL RULES FOR THE SIMULATION BASED ON AGE AND RULES FOR MOVEMENT.
late() {
	all = sim.subpopulations.individuals;
	is_female = all.tagF == 1.0;
	females = all[is_female];
	males = all[!is_female];
	
	males.fitnessScaling = 0.0; //males don't live on after reproducing
	females.fitnessScaling = 0.0; //most females also will die after reproducing
	
	//may allow some females to live on
	if (RATE_FEMALES_SURVIVE > 0.0) {
		num_survive = RATE_FEMALES_SURVIVE*size(females);
		fem_survive = sample(females, num_survive);
		fem_survive.fitnessScaling = 1.0;
	}
	
	surviving_inds = all[all.fitnessScaling > 0.0];
	
	//surviving individuals move
	for (ind in surviving_inds) {
		if (REPRISING_BOUNDARIES) {
			// Movement within reprising boundaries.
			do position = ind.spatialPosition + rnorm(1, 0, FEMALE_SPEED);
			while (!ind.subpopulation.pointInBounds(position));
			ind.setSpatialPosition(position);
		}
		else {
			// Torroidal world movement.
			ind.x = ind.x + rnorm(1, 0, FEMALE_SPEED);
			ind.setSpatialPosition(p1.pointPeriodic(ind.spatialPosition));
		}
	}
}


////SET UP THE POPULATION
1 {
	if (!WT_START) {
		sim.addSubpop("p1", CAPACITY);
		sim.setValue("timer", "start");
		sim.setValue("track_eq", NULL);
		all = p1.individuals;
		
		all.tagF = asFloat(rdunif(CAPACITY, 0, 1)); //random genders
		all.genomes.addNewDrawnMutation(m1, seqLen(NUM_GRNAS)); //all start wt
		all.x = p1.pointUniform(CAPACITY); //scattered randomly
		
		//drop wt into empty space
	} else {
		sim.addSubpop("p1", DROP_SIZE);
		sim.setValue("timer", "start");
		all = p1.individuals;
		all.genomes.addNewDrawnMutation(m1, seqLen(NUM_GRNAS));
		all.x = runif(DROP_SIZE,0,DROP_RADIUS);
	}
	
	if (MALE_MATE_CAP) {
		males = all[all.tagF == 0.0];
		males.setValue("mates", 0);
	}
}

//for wt starts, track time from population to reach 50%+ in bins [0.25-0.3] to bin [0.75-0.8]
1: late() {
	if (!WT_START)
		return;
	
	inds = p1.individuals;
	x = inds.x;
	at_start = (x >= 0.25) & (x <= 0.3);
	inds_start = inds[at_start];
	timer = sim.getValue("timer");
	
	//only start timer once
	if (size(inds_start) >= EQUILIBRIUM_BIN_POPULATION) {
		if (timer == "start") {
			catn("starting timer at " + sim.generation);
			sim.setValue("timer", sim.generation);
		}
	}
	
	at_end = (x >= 0.75) & (x <= 0.8);
	inds_end = inds[at_end];
	
	if (size(inds_end) >= EQUILIBRIUM_BIN_POPULATION) {
		start_gen = sim.getValue("timer");
		this_gen = sim.generation;
		gens_elapsed = this_gen - start_gen;
		catn("stopping timer at " + this_gen);
		catn("TIMED_GENS:: " + gens_elapsed);
		sim.simulationFinished();
	}
}


////DROP IN GENERATION 10
10 {
	if (WT_START | NO_DROP)
		return;
	
	sim.addSubpop("p2", DROP_SIZE);
	drop = p2.individuals;
	drop.color = "firebrick";
	
	if (MALE_ONLY_DROP)
		drop.tagF = rep(0.0, DROP_SIZE); //tag value of 0 acts as male sex
	
	else if (FEMALE_ONLY_DROP)
		drop.tagF = rep(1.0, DROP_SIZE); //tag value of 0 acts as male sex
	
	else
		drop.tagF = asFloat(rdunif(DROP_SIZE, 0, 1)); //random sexes
	
	females = (drop.tagF == 1.0);
	males = (drop.tagF == 0.0);
	drop_males = drop[males];
	drop_females = drop[females];
	
	//only Y chromosome has the drive. All X chromosomes should be wt
	if (X_SHREDDER) {
		drop_females.genomes.addNewDrawnMutation(m1, seqLen(NUM_GRNAS)); //females cannot get the drive
		drop_males.genome1.addNewDrawnMutation(m1, seqLen(NUM_GRNAS)); //maternal genome gets wt
		drop_males.genome2.addNewDrawnMutation(m2, seqLen(NUM_GRNAS)); //paternal genome gets drive
	}
	
	//only X chromsomes can have the drive. All Y chromosomes should be wt
	if (Y_SHREDDER) {
		drop_females.genomes.addNewDrawnMutation(m2, seqLen(NUM_GRNAS));
		
		if (HETEROZYGOUS_DROP) //if this is a heterozygous drop, drive females are (X^d X^wt)
			drop_females.genome2.addNewDrawnMutation(m1, seqLen(NUM_GRNAS));
		
		drop_males.genome1.addNewDrawnMutation(m2, seqLen(NUM_GRNAS)); //maternal genome is X; gets X^d
		drop_males.genome2.addNewDrawnMutation(m1, seqLen(NUM_GRNAS)); //paternal genome is a Y; stays wt
	}
	
	p1.takeMigrants(drop);
	drop.x = runif(DROP_SIZE, 0, DROP_RADIUS);
	
	if (MALE_MATE_CAP)
		drop_males.setValue("mates", 0);

	if (TRACK_RIPLEYS_L) {
		sim.setValue("wts", NULL);
		sim.setValue("L", NULL);
	}

}

////OUTPUT IF DROP IS RELEASED
10: late() {
	
	if (WT_START)
		return;
	
	all = p1.individuals;
	x = all.x;
	at_start = (x >= 0.25) & (x <= 0.3);
	inds_start = all[at_start];
	timer = sim.getValue("timer");
	this_gen = sim.generation - 10; //drop is generation 0
	
	//only start timer once
	if (size(inds_start) <= EQUILIBRIUM_BIN_POPULATION) {
		if (timer=="start") {
			catn("starting timer at " + this_gen);
			sim.setValue("timer", this_gen);
		}
	}
	
	at_end = (x >= 0.75) & (x <= 0.8);
	inds_end = all[at_end];
	
	if (size(inds_end) <= EQUILIBRIUM_BIN_POPULATION) {
		if (timer!="stopped" & timer!= "start") {
			start_gen = sim.getValue("timer");
			gens_elapsed = this_gen - start_gen;
			catn("stopping timer at " + this_gen);
			catn("TIMED_GENS:: " + gens_elapsed);
			sim.setValue("timer", "stopped"); //stop tracking
		}
	}
	
	//counts
	is_male = all.tagF==0.0;
	males = all[is_male];
	females = all[!is_male];
	pop_size = size(all);
	num_females = size(females);
	num_males = size(males);
	
	//count of chromosomes
	num_y = num_males;
	num_x = 2*num_females + num_males;
	num_autosomal_chromosomes = 2*pop_size;
	
	//allele counts common to both
	num_wt_x = sum(males.genome1.countOfMutationsOfType(m1)==NUM_GRNAS) + sum(females.genomes.countOfMutationsOfType(m1)==NUM_GRNAS);
	rate_wt_x = num_wt_x/num_x;
	
	//check for a potential chase
	if (num_wt_x < EQUILIBRIUM_WT_ALLELE_THRESHOLD)
		catn("POTENTIAL_CHASE:: " + this_gen);
	
	
	if (X_SHREDDER) {
		num_dr = sum(males.genome2.countOfMutationsOfType(m2)==NUM_GRNAS);
		rate_dr = num_dr/num_y;
	} else if (Y_SHREDDER) {
		num_dr = sum(males.genome1.countOfMutationsOfType(m2) == NUM_GRNAS) + sum(females.genomes.countOfMutationsOfType(m2)==NUM_GRNAS);
		rate_dr = num_dr/num_x;
	}
	
	if (!NO_DROP) {
		catn("GEN: " + this_gen + " RATE DR: " + rate_dr + " MALES to FEMALES " + num_males/num_females);
		catn("WT_ALLELES:: " + num_wt_x +" " + this_gen+ " " + pop_size); //for chasing analysis
	}

	if (TRACK_BY_CELL)  {
		inds = all[all.countOfMutationsOfType(m1) == 2*NUM_GRNAS];
		x = inds.x;
		y = inds.y;
		left_pos_vec = 0:(sqrt(NUMBER_OF_CELLS) - 1);
		cell_width = 1.0/length(left_pos_vec);
		
		for (pos_x in left_pos_vec) {
			div_left = pos_x * cell_width;
			for (pos_y in left_pos_vec) {
				div_bot = pos_y * cell_width;
				in_division = (x >= div_left) & (x < div_left + cell_width) & (y >= div_bot) & (y < div_bot + cell_width);
				inds_in_cell = inds[in_division];
				cell_count = size(inds_in_cell);
				hist_vec = c(cell_count, div_left, div_bot, this_gen);
				catn("count: " + cell_count + " x: " + div_left + " y: " + div_bot + " gen: " + this_gen);
				
				if (sim.generation == 0 & (pos_x == left_pos_vec[0]) & (pos_y == left_pos_vec[0])) {
					sim.setValue("hist", matrix(hist_vec, nrow = 1));
				} else {
					hist_mat = sim.getValue("hist");
					hist_mat = rbind(hist_mat, hist_vec);
					sim.setValue("hist", hist_mat);
				}
			}
		}
	}

	if (NO_DROP)
		return;
	
	//1-drive was lost and wt population remained
	if (num_dr == 0 & pop_size>0) {
		catn("POP_PERSISTS:: " + this_gen);
		
		if (TRACK_RIPLEYS_L)
			outputRipleysL("pers");
		if (TRACK_BY_CELL)
			outputCellCounts("pers");
		sim.simulationFinished();
	}
	
	//2-equilibrium state attained
	if (rate_dr== 1 & pop_size > 0) {
		start = sim.getValue("track_eq");
		if (isNULL(start)) {
			sim.setValue("track_eq", 1);
		} else if (start == 10) { //only allow state for 10 generations
			started_at = this_gen - 10;
			catn("EQUILIBRIUM:: " + started_at);
			
			if (TRACK_RIPLEYS_L)
				outputRipleysL("eq");
			
			if (TRACK_BY_CELL)
				outputCellCounts("eq");
			
			sim.simulationFinished();
		} else {
			elapsed = start + 1;
			sim.setValue("track_eq", elapsed);
		}
	}
	
	//3-suppression occurs
	if (pop_size == 0){
		catn("SUPPRESSED:: " + this_gen);
		if (TRACK_RIPLEYS_L)
			outputRipleysL("supp");
		if (TRACK_BY_CELL)
			outputCellCounts("supp");
		sim.simulationFinished();
	}
}

////output 500 generations after drop if simulation hasn't ended already
1010 late() {
	
	if (TRACK_RIPLEYS_L)
		outputRipleysL("end");
	if (TRACK_BY_CELL)
		outputCellCounts("end");
	
	all = p1.individuals;
	all_males = all[all.tagF==0.0];
	has_drive = sum(all.countOfMutationsOfType(m2) >= NUM_GRNAS);
	rate_has_drive = has_drive/size(all_males);
	
	catn("ENDING_AFTER_1000:: " + rate_has_drive);
	sim.simulationFinished();
}
